#include "Scroll.zxbas"

Dim attribsAlienA(6) as ubyte = { 0, 1, 65, 3, 4, 68 }
Dim attribsAlienB(4) as ubyte = { 66, 2, 1, 0 }
Dim colorsExp(4) as ubyte => { 66, 2, 1, 0  }

sub printZoomedTile(x as uByte, y as uByte, tileIndex as uinteger, attribute as ubyte)

    dim tbyte as ubyte
    dim tmpX as ubyte
    dim tmpY as ubyte

    Dim onColor as uByte = attribute band 7
    onColor = onColor bor (onColor << 3) bor (attribute band 192)

    Dim offColor as uByte = attribute band 56
    offColor = offColor bor (offColor >> 3) bor (attribute band 192)

    dim tileAddress as uinteger = tileIndex << 3
    tileAddress = tileAddress + @tileSet

    for tmpY = y to y + 7

        tbyte = peek(tileAddress)
        tmpX = x

        if tbyte band 128 then SetTileColor(tmpX, tmpY, onColor) else SetTileColor(tmpX, tmpY, offColor)
        tmpX = tmpX + 1
        if tbyte band 64 then SetTileColor(tmpX, tmpY, onColor) else SetTileColor(tmpX, tmpY, offColor)
        tmpX = tmpX + 1
        if tbyte band 32 then SetTileColor(tmpX, tmpY, onColor) else SetTileColor(tmpX, tmpY, offColor)
        tmpX = tmpX + 1
        if tbyte band 16 then SetTileColor(tmpX, tmpY, onColor) else SetTileColor(tmpX, tmpY, offColor)
        tmpX = tmpX + 1
        if tbyte band 8 then SetTileColor(tmpX, tmpY, onColor) else SetTileColor(tmpX, tmpY, offColor)
        tmpX = tmpX + 1
        if tbyte band 4 then SetTileColor(tmpX, tmpY, onColor) else SetTileColor(tmpX, tmpY, offColor)
        tmpX = tmpX + 1
        if tbyte band 2 then SetTileColor(tmpX, tmpY, onColor) else SetTileColor(tmpX, tmpY, offColor)
        tmpX = tmpX + 1
        if tbyte band 1 then SetTileColor(tmpX, tmpY, onColor) else SetTileColor(tmpX, tmpY, offColor)
        
        tileAddress = tileAddress + 1

    next tmpY

end sub

sub ShowZoomedFrame(index as ubyte, color as ubyte)

    printZoomedTile(3, 4, index, color)
    printZoomedTile(11, 4, index + 1, color)
    printZoomedTile(3, 12, index + 2, color)
    printZoomedTile(11, 12, index + 3, color)

end sub

sub ShowZoomedColoredFrame(index as ubyte)

    printZoomedTile(3, 4, index, tileColors(index + 1))
    printZoomedTile(11, 4, index + 1, tileColors(index + 2))
    printZoomedTile(3, 12, index + 2, tileColors(index + 3))
    printZoomedTile(11, 12, index + 3, tileColors(index + 4))

end sub

sub ShowLogoFrame(index as ubyte)

    printZoomedTile(3, 4, index, tileColors(index + 1))
    printZoomedTile(3, 12, index + 1, tileColors(index + 2))

    printZoomedTile(11, 4, index + 4, tileColors(index + 5))
    printZoomedTile(11, 12, index + 5, tileColors(index + 6))

    printZoomedTile(19, 4, index + 8, tileColors(index + 9))
    printZoomedTile(19, 12, index + 9, tileColors(index + 10))

end sub

sub PlayDeathAlien()

    Dim buc as ubyte

    for buc = 1 to 6

        ShowZoomedFrame(ZOOMALIENA_INDEX, attribsAlienA(buc))

    next buc

    DoPause(50)

    ShowZoomedFrame(ZOOMALIENB_INDEX, 68)

    AlienDeathEffect()
    DoPause(5)

    for buc = 1 to 4

        ShowZoomedFrame(ZOOMALIENB_INDEX, attribsAlienB(buc))

    next buc

    DoPause(50)

end sub

sub PlayDeathShot()

    Dim currentIndex as ubyte
    Dim buc as ubyte

    currentIndex = SHOTDEATHA_INDEX

    ShowZoomedFrame(SHOTDEATHA_INDEX, 6)

    DoPause(30)

    for buc = 1 to 7

        ShowZoomedFrame(currentIndex, tileColors(currentIndex + 1))
        if buc < 3 then
            DoPause(7)
        else
            ExplodeAnimEffect()
        end if

        currentIndex = currentIndex + 4

    next buc

    for buc = 1 to 4

        ShowZoomedFrame(SHOTDEATHG_INDEX, colorsExp(buc))
        DoPause(10)

    next buc

    DoPause(50)

end sub

sub PlayDeathTank()

    ShowZoomedFrame(TANKDEATHA_INDEX, 1)
    DoPause(10)
    ShowZoomedFrame(TANKDEATHA_INDEX, 7)
    DoPause(10)
    ShowZoomedFrame(TANKDEATHA_INDEX, 71)
    DoPause(30)
    ShowZoomedFrame(TANKDEATHB_INDEX, 114)
    'DoPause(15)
    ExplodeAnimEffect()


    ShowZoomedFrame(TANKDEATHA_INDEX, 71)
    DoPause(30)
    ShowZoomedFrame(TANKDEATHA_INDEX, 0)
    DoPause(10)

    PlayDeathShot()

end sub

sub PlayAlienTalk()

    ShowZoomedColoredFrame(ALIENTALKA_INDEX)
    DoPause(10)
    ShowZoomedColoredFrame(ALIENTALKB_INDEX)
    AlienTalkEffect()
    ShowZoomedColoredFrame(ALIENTALKA_INDEX)
    DoPause(15)
    ShowZoomedColoredFrame(ALIENTALKB_INDEX)
    AlienTalkEffect()
    ShowZoomedColoredFrame(ALIENTALKA_INDEX)
    DoPause(1)
    ShowZoomedColoredFrame(ALIENTALKB_INDEX)
    AlienTalkEffect()
    ShowZoomedColoredFrame(ALIENTALKA_INDEX)
    DoPause(10)

end sub

sub PlayComputerThink()

    ShowZoomedColoredFrame(COMPUTERA_INDEX)
    ComputerThinkAEffect()
    ShowZoomedColoredFrame(COMPUTERB_INDEX)
    DoPause(2)
    ShowZoomedColoredFrame(COMPUTERA_INDEX)
    ComputerThinkBEffect()
    ShowZoomedColoredFrame(COMPUTERB_INDEX)
    DoPause(10)

end sub

sub PlayAlienListen()

    ShowZoomedColoredFrame(ALIENTALKC_INDEX)
    DoPause(10)
    ShowZoomedColoredFrame(ALIENTALKD_INDEX)
    AlienListenEffect()
    ShowZoomedColoredFrame(ALIENTALKC_INDEX)
    DoPause(15)
    ShowZoomedColoredFrame(ALIENTALKD_INDEX)
    AlienListenEffect()
    ShowZoomedColoredFrame(ALIENTALKC_INDEX)
    DoPause(1)
    ShowZoomedColoredFrame(ALIENTALKD_INDEX)
    AlienListenEffect()
    ShowZoomedColoredFrame(ALIENTALKC_INDEX)
    DoPause(50)

end sub

sub PlayAlienTeleport()

    ShowZoomedColoredFrame(TELEPORTA_INDEX)
    DoPause(10)
    Teleportffect()
    ShowZoomedColoredFrame(TELEPORTB_INDEX)
    Teleportffect()
    ShowZoomedColoredFrame(TELEPORTC_INDEX)
    Teleportffect()
    ShowZoomedColoredFrame(TELEPORTD_INDEX)
    Teleportffect()
    printZoomedTile(3, 4, EMPTY_TILE_INDEX, 0)
    printZoomedTile(11, 4, EMPTY_TILE_INDEX, 0)
    printZoomedTile(3, 12, EMPTY_TILE_INDEX, 0)
    printZoomedTile(11, 12, EMPTY_TILE_INDEX, 0)
    DoPause(10)

end sub

sub PlayIntro()

    ShowLogoFrame(LOGO_INDEX)
    Introffect()
    ShowLogoFrame(LOGO_INDEX + 4)
    Introffect()
    ShowLogoFrame(LOGO_INDEX + 8)
    Introffect()
    ShowLogoFrame(LOGO_INDEX + 12)
    Introffect()
    ShowLogoFrame(LOGO_INDEX + 16)
    Introffect()
    ShowLogoFrame(LOGO_INDEX + 20)
    Introffect()


    ShowLogoFrame(LOGO_INDEX + 2)
    Introffect()
    ShowLogoFrame(LOGO_INDEX + 6)
    Introffect()
    ShowLogoFrame(LOGO_INDEX + 10)
    Introffect()
    ShowLogoFrame(LOGO_INDEX + 14)
    Introffect()
    ShowLogoFrame(LOGO_INDEX + 18)
    Introffect()
    ShowLogoFrame(LOGO_INDEX + 22)
    Introffect()

end sub

sub PlayShowText(textIndex as uByte)

    ClearScreen(7)

    DoPause(6)

    printZoomedTile(4, 4, textIndex + 2, tileColors(textIndex + 3))
    printZoomedTile(20, 12, textIndex + 3, tileColors(textIndex + 4))

    DoPause(6)

    printZoomedTile(4, 4, textIndex + 1, tileColors(textIndex + 2))
    printZoomedTile(20, 12, textIndex + 4, tileColors(textIndex + 5))

    printZoomedTile(12, 4, textIndex + 2, tileColors(textIndex + 3))
    printZoomedTile(12, 12, textIndex + 3, tileColors(textIndex + 4))

    DoPause(6)

    printZoomedTile(4, 4, textIndex, tileColors(textIndex + 1))
    printZoomedTile(20, 12, textIndex + 5, tileColors(textIndex + 6))

    printZoomedTile(12, 4, textIndex + 1, tileColors(textIndex + 2))
    printZoomedTile(12, 12, textIndex + 4, tileColors(textIndex + 5))

    printZoomedTile(20, 4, textIndex + 2, tileColors(GRIDCLEAR_INDEX + 3))
    printZoomedTile(4, 12, textIndex + 3, tileColors(GRIDCLEAR_INDEX + 4))

    DoPause(140)

end sub

sub PlayLevel(level as uByte)

    Dim num1 as uByte
    Dim num2 as uByte

    Dim color as uByte

    ClearScreen(7)

    DoPause(2)

    for color = 1 to 6

        printZoomedTile(4, 4, LEVEL_INDEX, CREATE_ATTRIB(color, 0, 1, 0))
        printZoomedTile(12, 4, LEVEL_INDEX + 1, CREATE_ATTRIB(color, 0, 1, 0))
        printZoomedTile(20, 4, LEVEL_INDEX + 2, CREATE_ATTRIB(color, 0, 1, 0))
   
        DoPause(2)

    next color
    
    DoPause(8)

    if level < 10 then
        num1 = 0
        num2 = level
    else
        num1 = level / 10
        num2 = level mod 10
    end if

    for color = 1 to 2

        printZoomedTile(8, 12, NUMBERS_INDEX + num1, CREATE_ATTRIB(color, 0, 1, 0))
        printZoomedTile(16, 12, NUMBERS_INDEX + num2, CREATE_ATTRIB(color, 0, 1, 0))

        DoPause(4)

    next color

    DoPause(140)

end sub

sub PlayEnding()

    ClearScreen(7)

    Dim offset as uByte
    Dim buc as uByte

    for buc = 0 to 16 step 16

        printZoomedTile(4, buc, ENDINGFIXED_INDEX + offset, tileColors(ENDINGFIXED_INDEX + offset + 1))
        printZoomedTile(12, buc, ENDINGFIXED_INDEX + 1 + offset, tileColors(ENDINGFIXED_INDEX + offset + 2))
        printZoomedTile(20, buc, ENDINGFIXED_INDEX + 2 + offset, tileColors(ENDINGFIXED_INDEX + offset + 3))

        offset = 3

    next buc

    DoPause (60)

    for buc = 0 to 8

        offset = buc * 3

        printZoomedTile(4, 8, ENDINGANIMA_INDEX + offset, tileColors(ENDINGANIMA_INDEX + offset + 1))
        printZoomedTile(12, 8, ENDINGANIMA_INDEX + 1 + offset, tileColors(ENDINGANIMA_INDEX + offset + 2))
        printZoomedTile(20, 8, ENDINGANIMA_INDEX + 2 + offset, tileColors(ENDINGANIMA_INDEX + offset + 3))

        DoPause (5)

    next buc

    for buc = 4 to 28
        for offset = 8 to 15
            SetTileColor(buc, offset, 7)
        next offset
    next buc

    DoPause (60)

    Dim tmpString as string

    for buc = 1 to 13

        if buc = 1 then
            tmpString = "The last alien units"
        else if buc = 2 then
            tmpString = "are leaving the earth."
        else if buc = 4 then
            tmpString = "Thanks to you the planet"
        else if buc = 5 then
            tmpString = "is safe again."
        else if buc = 7 then
            tmpString = "        GAME OVER"
        else if buc = 9 then
            tmpString = "        (C)  2021"
        else if buc = 10 then
            tmpString = "      El Dr. Gusman"
        else
            tmpString = " "
        end if

        BLWindowScrollUp(4, 8, 24, 8)
        PrintString(tmpString, CREATE_ATTRIB(7, 0, 1, 0), 4, 15)
        DoPause (60)
    next buc

    DoPause(140)

end sub