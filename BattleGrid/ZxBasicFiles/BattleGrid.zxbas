REM @options --asm --array-base 1 --string-base 1 --optimize 4 --org 32768 --heap-size 256 --explicit --strict -D HIDE_LOAD_MSG

#define PRECOMPUTED_SPRITES
#define SPRITEDATA_FILE "Sprites.zxbas"

#define ENABLE_1x1_SPRITES
'#define ENABLE_1x2_SPRITES
'#define ENABLE_2x2_SPRITES

'Total number of 1x1 defined sprites
#define TOTAL_1x1_SPRITES 16
'Total number of 1x2 defined sprites
#define TOTAL_1x2_SPRITES 0
'Total number of 2x2 defined sprites
#define TOTAL_2x2_SPRITES 0

'Maximum on-screen 1x1 sprites
#define ONSCREEN_1x1_SPRITES 10
'Maximum on-screen 1x2 sprites
#define ONSCREEN_1x2_SPRITES 0
'Maximum on-screen 2x2 sprites
#define ONSCREEN_2x2_SPRITES 0

'If defined enables the tile system in its basic mode (tiles erased when sprite enters on it)
#define ENABLE_TILES
'If defined tiles are merged with OR instead of erased
'Requires ENABLE_TILES
#define MERGE_TILES

'The lib by default disables interrupts, enabling this option
'will reactivate them after each call to the lib
#define ENABLE_INTERRUPTS

#include "GuSprites.zxbas"
#include <keys.bas>
#include "FastRND.zxbas"
#include "BeepFX.zxbas"
#include "InterruptInstaller.zxbas"

/' IDEAS

    Espejos morados, solo soportan un disparo
    Divisores azules, solo soportan un disparo
    Los bloqueadores se destruyen cuando un enemigo los toca
    Generadores de enemigos
    Celdas donde no se acepten poner objetos

'/
/'

'REM --SPRITE SECTION--
#define PLAYER_INDEX 1
#define PLAYERRIGHT_INDEX 2
#define PLAYERLEFT_INDEX 3
#define VFIREA_INDEX 4
#define VFIREB_INDEX 5
#define HFIREA_INDEX 6
#define HFIREB_INDEX 7
#define EXPLODEA_INDEX 8
#define EXPLODEB_INDEX 9
#define EXPLODEC_INDEX 10
#define EXPLODED_INDEX 11
#define CURSOR_INDEX 12
#define ENEMA_INDEX 13
#define ENEMB_INDEX 14
#define TANKV_INDEX 15
#define TANKH_INDEX 16

'REM --SPRITE SECTION--
#define PLAYER_ADDRESS (@spriteSet + 0)
#define PLAYERRIGHT_ADDRESS (@spriteSet + 8)
#define PLAYERLEFT_ADDRESS (@spriteSet + 16)
#define VFIREA_ADDRESS (@spriteSet + 24)
#define VFIREB_ADDRESS (@spriteSet + 32)
#define HFIREA_ADDRESS (@spriteSet + 40)
#define HFIREB_ADDRESS (@spriteSet + 48)
#define EXPLODEA_ADDRESS (@spriteSet + 56)
#define EXPLODEB_ADDRESS (@spriteSet + 64)
#define EXPLODEC_ADDRESS (@spriteSet + 72)
#define EXPLODED_ADDRESS (@spriteSet + 80)
#define CURSOR_ADDRESS (@spriteSet + 88)
#define ENEMA_ADDRESS (@spriteSet + 96)
#define ENEMB_ADDRESS (@spriteSet + 104)
#define TANKV_ADDRESS (@spriteSet + 112)
#define TANKH_ADDRESS (@spriteSet + 120)

Dim spriteSet(16,8) as uByte => { _
{ $18, $5A, $A5, $BD, $7E, $66, $5A, $A5 }, _
{ $08, $2A, $14, $1C, $3E, $36, $2A, $55 }, _
{ $10, $54, $28, $38, $7C, $6C, $54, $AA }, _
{ $21, $18, $85, $18, $21, $18, $84, $18 }, _
{ $18, $84, $18, $A1, $18, $84, $18, $21 }, _
{ $15, $00, $44, $AA, $AA, $11, $00, $44 }, _
{ $88, $00, $22, $55, $55, $88, $00, $2A }, _
{ $00, $00, $18, $3C, $3C, $18, $00, $00 }, _
{ $00, $18, $24, $5A, $5A, $24, $18, $00 }, _
{ $3C, $42, $99, $A5, $A5, $99, $42, $3C }, _
{ $42, $81, $24, $00, $00, $24, $81, $42 }, _
{ $E7, $81, $81, $00, $00, $81, $81, $E7 }, _
{ $24, $7E, $FF, $99, $DB, $FF, $66, $24 }, _
{ $24, $7E, $FF, $BD, $DB, $7E, $7E, $18 }, _
{ $5A, $7E, $66, $5A, $5A, $66, $5A, $18 }, _
{ $FF, $66, $DB, $DB, $66, $FF, $18, $18 } _
}


'REM --TILE SECTION--
#define EMPTY_TILE_INDEX 0
#define LOGO_INDEX 1
#define GRID_INDEX 33
#define DEFLECTORA_INDEX 42
#define DEFLECTORB_INDEX 43
#define DEFLECTORC_INDEX 44
#define DEFLECTORD_INDEX 45
#define HBARRIER_INDEX 46
#define VBARRIER_INDEX 49
#define BIGBUILDING_INDEX 52
#define MEDIUMBUILDING_INDEX 61
#define TALLBUILDING_INDEX 67
#define WIDEBUILDING_INDEX 69
#define SMALLBUILDING_INDEX 71
#define EXPLOSIONCRATER_INDEX 72
#define TOXICWASTE_INDEX 81
#define SPLITTER_INDEX 82
#define BLOCKER_INDEX 83
#define MINE_INDEX 84
#define BOMB_INDEX 85
#define CHARGEDSHOT_INDEX 86
#define MINECRATE_INDEX 87
#define BLOCKERCRATE_INDEX 88
#define BOMBCRATE_INDEX 89
#define CHARGEDSHOTCRATE_INDEX 90
#define RADIOTOWER_INDEX 91
#define FORCEFIELD_INDEX 92
#define MOTHERSHIP_INDEX 93
#define HANGAR_INDEX 94
#define NUMBERS_INDEX 95
#define SOLID_INDEX 105
#define ZOOMALIENA_INDEX 106
#define ZOOMALIENB_INDEX 110
#define SHOTDEATHA_INDEX 114
#define SHOTDEATHB_INDEX 118
#define SHOTDEATHC_INDEX 122
#define SHOTDEATHD_INDEX 126
#define SHOTDEATHE_INDEX 130
#define SHOTDEATHF_INDEX 134
#define SHOTDEATHG_INDEX 138
#define TANKDEATHA_INDEX 142
#define TANKDEATHB_INDEX 146
#define LIFE_INDEX 150

Dim tileSet(151,8) as uByte => { _
{ $00, $00, $00, $00, $00, $00, $00, $00 }, _
{ $00, $0F, $07, $07, $07, $07, $07, $07 }, _
{ $07, $07, $07, $07, $0F, $00, $00, $FC }, _
{ $FC, $00, $00, $00, $00, $0C, $0C, $1E }, _
{ $7F, $1E, $3F, $33, $40, $00, $00, $00 }, _
{ $00, $78, $1C, $1C, $1C, $18, $70, $38 }, _
{ $1C, $1C, $1D, $1D, $7B, $00, $00, $00 }, _
{ $03, $0C, $1C, $1C, $3C, $3C, $3C, $3D }, _
{ $BD, $3C, $3C, $1C, $9C, $0C, $03, $00 }, _
{ $00, $78, $78, $B8, $B8, $B8, $BC, $BC }, _
{ $BC, $FC, $1E, $1E, $9F, $00, $00, $00 }, _
{ $D3, $71, $31, $31, $11, $11, $01, $F9 }, _
{ $F1, $F1, $F1, $F1, $F1, $E1, $83, $00 }, _
{ $00, $DD, $DD, $9C, $9C, $1C, $1C, $1C }, _
{ $1C, $1C, $1C, $1C, $3E, $00, $00, $00 }, _
{ $EE, $E7, $E7, $E7, $E7, $E7, $EE, $EE }, _
{ $E7, $E7, $E7, $E7, $E7, $E7, $F3, $00 }, _
{ $00, $EE, $EE, $CE, $CE, $0E, $0E, $0E }, _
{ $0E, $0E, $0E, $1E, $1F, $00, $00, $00 }, _
{ $1F, $0F, $8F, $8F, $8F, $0F, $0F, $0F }, _
{ $0F, $8F, $8F, $8F, $CF, $CF, $9F, $00 }, _
{ $00, $CF, $C7, $47, $47, $07, $07, $07 }, _
{ $07, $07, $07, $07, $0F, $00, $00, $00 }, _
{ $BF, $1F, $0F, $0F, $0F, $0F, $0F, $0F }, _
{ $0F, $0F, $0F, $0F, $0F, $0F, $9F, $00 }, _
{ $00, $0F, $07, $07, $07, $07, $07, $07 }, _
{ $07, $27, $27, $67, $6F, $00, $00, $00 }, _
{ $60, $30, $38, $38, $3C, $3C, $3C, $3C }, _
{ $3D, $3C, $3C, $38, $39, $30, $40, $00 }, _
{ $00, $60, $60, $20, $00, $40, $40, $40 }, _
{ $40, $00, $20, $60, $60, $00, $00, $3F }, _
{ $3F, $00, $00, $00, $00, $30, $30, $78 }, _
{ $FE, $78, $FC, $CC, $02, $00, $00, $00 }, _
{ $3F, $40, $AF, $98, $B3, $A4, $A9, $AA }, _
{ $AA, $A2, $A2, $AA, $AA, $A2, $A2, $AA }, _
{ $AA, $A9, $A4, $B3, $98, $AF, $40, $3F }, _
{ $FF, $00, $FF, $00, $99, $00, $FF, $00 }, _
{ $00, $00, $00, $18, $18, $00, $00, $00 }, _
{ $00, $FF, $00, $99, $00, $FF, $00, $FF }, _
{ $FC, $02, $F5, $19, $CD, $25, $95, $55 }, _
{ $55, $45, $45, $55, $55, $45, $45, $55 }, _
{ $55, $95, $25, $CD, $19, $F5, $02, $FC }, _
{ $80, $C0, $E0, $B0, $B8, $BC, $C6, $FF }, _
{ $FF, $C6, $BC, $B8, $B0, $E0, $C0, $80 }, _
{ $01, $03, $07, $0D, $1D, $3D, $63, $FF }, _
{ $FF, $63, $3D, $1D, $0D, $07, $03, $01 }, _
{ $42, $A5, $7E, $2F, $37, $7E, $A5, $42 }, _
{ $00, $99, $66, $DB, $BD, $66, $99, $00 }, _
{ $42, $A5, $7E, $F4, $EC, $7E, $A5, $42 }, _
{ $42, $A5, $7E, $34, $2C, $7E, $BD, $5A }, _
{ $5A, $24, $34, $5A, $5A, $34, $24, $5A }, _
{ $5A, $BD, $7E, $2C, $34, $7E, $A5, $42 }, _
{ $00, $02, $03, $07, $09, $09, $1F, $09 }, _
{ $00, $7E, $FF, $FF, $24, $24, $FF, $24 }, _
{ $00, $40, $C0, $F0, $90, $90, $FC, $92 }, _
{ $09, $3F, $61, $7F, $09, $09, $3F, $49 }, _
{ $24, $FF, $00, $FF, $24, $24, $FF, $24 }, _
{ $92, $FE, $82, $FE, $92, $92, $FC, $90 }, _
{ $49, $7F, $41, $7F, $7F, $67, $67, $27 }, _
{ $24, $FF, $3C, $E7, $DB, $A5, $A5, $A5 }, _
{ $90, $FE, $82, $FE, $FE, $E6, $E4, $E0 }, _
{ $00, $00, $03, $03, $06, $05, $27, $36 }, _
{ $3C, $3C, $E7, $A5, $BD, $E7, $A5, $BD }, _
{ $00, $7C, $82, $CA, $6A, $A2, $FA, $6A }, _
{ $76, $5E, $5A, $7F, $27, $2F, $D8, $3F }, _
{ $E7, $A5, $BD, $E7, $FF, $E7, $C3, $C3 }, _
{ $6E, $7E, $5E, $FE, $E6, $F6, $1E, $FE }, _
{ $60, $F0, $A0, $B8, $A4, $BC, $A6, $BD }, _
{ $A5, $BD, $A5, $BD, $A5, $FD, $E7, $E7 }, _
{ $28, $3E, $26, $E7, $BE, $E7, $A6, $FE }, _
{ $06, $1F, $05, $C7, $7D, $E7, $65, $7F }, _
{ $16, $1F, $85, $DF, $A5, $FF, $A5, $E7 }, _
{ $FF, $FF, $FF, $FC, $F6, $D6, $E7, $C5 }, _
{ $FF, $FB, $3B, $57, $EB, $83, $00, $00 }, _
{ $FF, $FB, $BF, $FF, $BF, $53, $BF, $BF }, _
{ $EA, $B8, $B8, $80, $B0, $F4, $C8, $BC }, _
{ $34, $7A, $FB, $ED, $F7, $CD, $7A, $24 }, _
{ $7B, $77, $2F, $1F, $1F, $3F, $3F, $1F }, _
{ $BC, $DA, $D8, $F7, $E3, $FB, $FF, $FF }, _
{ $00, $80, $CF, $CE, $92, $74, $77, $DF }, _
{ $67, $FF, $1D, $DF, $F5, $EB, $5F, $FF }, _
{ $80, $24, $7E, $3C, $11, $04, $4A, $24 }, _
{ $24, $3C, $DB, $7E, $7E, $DB, $3C, $24 }, _
{ $42, $ED, $DB, $B7, $42, $7E, $42, $42 }, _
{ $00, $00, $00, $00, $3C, $7E, $00, $00 }, _
{ $24, $18, $3C, $5A, $42, $5A, $3C, $18 }, _
{ $19, $22, $98, $44, $19, $22, $98, $44 }, _
{ $E7, $81, $81, $18, $3C, $81, $81, $E7 }, _
{ $E7, $81, $BD, $3C, $24, $A5, $81, $E7 }, _
{ $E7, $81, $A5, $18, $3C, $99, $81, $E7 }, _
{ $E7, $81, $91, $48, $12, $89, $81, $E7 }, _
{ $5A, $24, $5A, $18, $24, $3C, $66, $DB }, _
{ $FF, $00, $FF, $55, $AA, $FF, $00, $FF }, _
{ $3C, $42, $FF, $3C, $00, $18, $00, $7E }, _
{ $18, $FF, $00, $99, $99, $00, $FF, $18 }, _
{ $00, $3C, $72, $72, $42, $42, $3C, $00 }, _
{ $00, $18, $38, $08, $08, $08, $08, $00 }, _
{ $00, $7E, $62, $02, $7E, $40, $7E, $00 }, _
{ $00, $7E, $62, $02, $1E, $42, $7E, $00 }, _
{ $00, $1C, $2C, $44, $7C, $04, $04, $00 }, _
{ $00, $7E, $46, $40, $7E, $02, $7E, $00 }, _
{ $00, $7E, $40, $7E, $72, $62, $7E, $00 }, _
{ $00, $7E, $62, $04, $08, $18, $18, $00 }, _
{ $00, $3C, $2C, $24, $7E, $62, $7E, $00 }, _
{ $00, $7E, $62, $7E, $02, $06, $7E, $00 }, _
{ $FF, $FF, $FF, $FF, $FF, $FF, $FF, $FF }, _
{ $00, $1F, $38, $67, $7E, $73, $61, $71 }, _
{ $00, $F8, $1C, $E6, $7E, $CE, $86, $8E }, _
{ $79, $3F, $37, $32, $30, $35, $1F, $0F }, _
{ $9E, $FC, $EC, $4C, $0C, $AC, $F8, $F0 }, _
{ $00, $1F, $39, $7E, $67, $61, $61, $61 }, _
{ $00, $F8, $9C, $7E, $E6, $86, $86, $86 }, _
{ $71, $3F, $27, $22, $20, $20, $15, $0F }, _
{ $8E, $FC, $E4, $44, $04, $04, $A8, $F0 }, _
{ $09, $1D, $0F, $06, $04, $07, $07, $0E }, _
{ $90, $B8, $F0, $60, $20, $E0, $E0, $70 }, _
{ $1C, $1F, $1E, $1C, $19, $1B, $66, $22 }, _
{ $38, $F8, $78, $38, $98, $D8, $66, $44 }, _
{ $09, $1D, $0F, $06, $04, $07, $07, $0E }, _
{ $90, $BA, $F0, $63, $26, $EC, $E2, $70 }, _
{ $1C, $1F, $1E, $1C, $19, $1B, $66, $22 }, _
{ $38, $F8, $78, $38, $98, $D8, $66, $44 }, _
{ $09, $1D, $0F, $06, $04, $07, $07, $0E }, _
{ $90, $B8, $F0, $60, $3C, $C2, $DA, $5A }, _
{ $1C, $1F, $1E, $1C, $19, $1B, $66, $22 }, _
{ $42, $BC, $78, $38, $98, $D8, $66, $44 }, _
{ $09, $1D, $0F, $07, $04, $04, $06, $0E }, _
{ $88, $99, $80, $3C, $42, $99, $A5, $A5 }, _
{ $1C, $1E, $1E, $1C, $19, $1B, $66, $22 }, _
{ $99, $42, $3C, $01, $98, $C8, $66, $44 }, _
{ $08, $18, $09, $02, $02, $00, $00, $08 }, _
{ $0C, $82, $39, $45, $3C, $7E, $F7, $6F }, _
{ $1A, $12, $12, $10, $19, $18, $66, $22 }, _
{ $7E, $BC, $99, $4E, $30, $82, $6C, $00 }, _
{ $21, $47, $06, $10, $19, $1A, $20, $28 }, _
{ $0C, $4E, $25, $43, $05, $00, $98, $19 }, _
{ $02, $32, $18, $19, $08, $42, $27, $01 }, _
{ $82, $30, $01, $41, $17, $80, $69, $C2 }, _
{ $00, $08, $00, $00, $20, $04, $00, $40 }, _
{ $00, $40, $04, $00, $02, $00, $00, $80 }, _
{ $00, $20, $04, $00, $40, $00, $08, $00 }, _
{ $00, $08, $00, $22, $00, $40, $08, $00 }, _
{ $4E, $4C, $78, $49, $4C, $76, $4B, $3F }, _
{ $72, $32, $1E, $92, $32, $6E, $D2, $7C }, _
{ $02, $03, $02, $03, $02, $03, $01, $00 }, _
{ $C0, $40, $C0, $40, $C0, $40, $80, $00 }, _
{ $76, $4B, $3F, $02, $03, $02, $03, $02 }, _
{ $6E, $D2, $7C, $C0, $40, $C0, $40, $C0 }, _
{ $03, $01, $04, $01, $11, $01, $45, $11 }, _
{ $40, $80, $20, $80, $88, $A0, $82, $90 }, _
{ $00, $66, $FF, $FF, $7E, $3C, $18, $00 } _
}

Dim tileColors(151) as ubyte => { $0, $42, $42, $46, $46, $42, $42, $46, $46, $42, $42, $46, $46, $42, $42, $46, $46, $42, $42, $46, $46, $42, $42, $46, $46, $42, $42, $46, $46, $42, $42, $46, $46, $41, $41, $41, $41, $47, $41, $41, $41, $41, $45, $45, $45, $45, $07, $47, $07, $07, $47, $07, $03, $03, $02, $03, $02, $02, $02, $02, $02, $03, $03, $02, $03, $02, $02, $03, $02, $03, $02, $02, $10, $10, $10, $10, $16, $10, $10, $10, $10, $06, $44, $03, $06, $42, $44, $06, $03, $42, $44, $05, $C6, $46, $47, $47, $47, $47, $47, $47, $47, $47, $47, $47, $47, $38, $04, $04, $04, $04, $04, $04, $04, $04, $06, $06, $06, $06, $46, $46, $46, $46, $72, $72, $72, $72, $56, $56, $56, $56, $56, $56, $56, $56, $56, $56, $56, $56, $56, $56, $56, $56, $07, $07, $07, $07, $72, $72, $72, $72, $02 }
'/

defines:
#define CURSOR_FRAMES 10
#define CURSOR_SPEED 5

#define MAP_X_OFFSET 0
#define MAP_Y_OFFSET 1

#define MAX_ALIEN_COUNT 4
#define ALIEN_SPEED 5

#define MAX_TANK_COUNT 2
#define TANK_SPEED 3

#define MAX_SHIP_COUNT 4
#define SHIP_SPEED 50

#define MAX_HANGAR_COUNT 2
#define HANGAR_SPEED 150

#define TOOL_NONE 0
#define TOOL_BLOCKER 1
#define TOOL_MINE 2
#define TOOL_BOMB 3
#define TOOL_CHARGEDSHOT 4


macros:
#define TABLE_LOOKUP(Array, Index) (peek (Array + Index - 1))

#define EXPLODE_SHOT(INDEX) shots(INDEX, 4) = 0

#define PRINT_DIGIT(X, Y, DIGIT) SetTile(NUMBERS_INDEX + DIGIT, tileColors(NUMBERS_INDEX + DIGIT + 1), X, Y)

#define INCREASE_BLOCKERS() availableBlockers = availableBlockers + 1 \
PRINT_DIGIT(26, 18, availableBlockers)
#define DECREASE_BLOCKERS() availableBlockers = availableBlockers - 1 \
PRINT_DIGIT(26, 18, availableBlockers)

#define INCREASE_MINES() availableMines = availableMines + 1 \
PRINT_DIGIT(30, 18, availableMines)
#define DECREASE_MINES() availableMines = availableMines - 1 \
PRINT_DIGIT(30, 18, availableMines)

#define INCREASE_BOMBS() availableBombs = availableBombs + 1 \
PRINT_DIGIT(26, 20, availableBombs)
#define DECREASE_BOMBS() availableBombs = availableBombs - 1 \
PRINT_DIGIT(26, 20, availableBombs)

#define INCREASE_SHOTS() availableChargedShots = availableChargedShots + 1 \
PRINT_DIGIT(30, 20, availableChargedShots)
#define DECREASE_SHOTS() availableChargedShots = availableChargedShots - 1 \
PRINT_DIGIT(30, 20, availableChargedShots)

#define AVAILABLE_SLOT(BITFIELD, MAXITEMS) (bitTable(MAXITEMS) bxor BITFIELD)

#define CREATE_ALIEN(X, Y, DIRECTION, TMPVAR) \
TMPVAR = FindEmptyBit(activeAliens) \
activeAliens = activeAliens or TABLE_LOOKUP(@maskTable, TMPVAR) \
aliens(TMPVAR, 1) = (X + MAP_X_OFFSET) << 1 \
aliens(TMPVAR, 2) = (Y + MAP_Y_OFFSET) << 1 \
aliens(TMPVAR, 3) = DIRECTION \
aliens(TMPVAR, 4) = -1 \


#define CREATE_TANK(X, Y, DIRECTION, TMPVAR) \
TMPVAR = FindEmptyBit(activeTanks) \
activeTanks = activeTanks or TABLE_LOOKUP(@maskTable, TMPVAR) \
tanks(TMPVAR, 1) = (X + MAP_X_OFFSET) << 1 \
tanks(TMPVAR, 2) = (Y + MAP_Y_OFFSET) << 1 \
tanks(TMPVAR, 3) = DIRECTION \
tanks(TMPVAR, 4) = -1 \

#define CREATE_SHIP(X, Y, SPAWNDIRECTION, TMPVAR) \
TMPVAR = FindEmptyBit(activeShips) \
activeShips = activeShips or TABLE_LOOKUP(@maskTable, TMPVAR) \
ships(TMPVAR, 1) = X \
ships(TMPVAR, 2) = Y \
ships(TMPVAR, 3) = SPAWNDIRECTION \
ships(TMPVAR, 4) = SHIP_SPEED \
GridObject(MOTHERSHIP_INDEX, X, Y)

#define CREATE_HANGAR(X, Y, SPAWNDIRECTION, TMPVAR) \
TMPVAR = FindEmptyBit(activeHangars) \
activeHangars = activeHangars or TABLE_LOOKUP(@maskTable, TMPVAR) \
hangars(TMPVAR, 1) = X \
hangars(TMPVAR, 2) = Y \
hangars(TMPVAR, 3) = SPAWNDIRECTION \
hangars(TMPVAR, 4) = HANGAR_SPEED \
GridObject(HANGAR_INDEX, X, Y)

#define CREATE_TOWER(X, Y) \
GridObject(RADIOTOWER_INDEX, X, Y) \
activeTowers = activeTowers + 1 \


vars:
Dim playerX as uByte = 20
Dim playerSprite as uByte = 0
Dim score as uInteger
Dim updateScore as uByte = 0
Dim lifes as uByte = 4

Dim dir as byte = 0
Dim dead as ubyte = 0           '1 = muerte por disparo, 2 = muerte por tanque, 3 = muerte por alien

Dim shots(3,4) as Byte         'x,y,dir,explodePhase
Dim activeShots as uByte

Dim cursorX as uByte = 0
Dim cursorY as uByte = 0
Dim cursorVisible as uByte = 0
Dim cursorFrames as uByte = CURSOR_FRAMES
Dim cursorMoved as uByte = 0
Dim cursorTool as uByte = TOOL_NONE

Dim activeMap(20, 20) as uByte

Dim reflectDirsA(4) as uByte => { 2, 4, 1, 3 }
Dim reflectDirsB(4) as uByte => { 4, 1, 2, 3 }
Dim reflectDirsC(4) as uByte => { 2, 3, 4, 1 }
Dim reflectDirsD(4) as uByte => { 3, 1, 4, 2 }

Dim maskTable(8) as uByte => { 1, 2, 4, 8, 16, 32, 64, 128  }
Dim bitTable(8) as uByte => { 1, 3, 7, 15, 31, 63, 127, 256  }

Dim aliens(MAX_ALIEN_COUNT,4) as Byte         'x,y,dir,explodePhase
Dim activeAliens as uByte
Dim alienFrame as uByte = ALIEN_SPEED

Dim tanks(MAX_TANK_COUNT,4) as Byte         'x,y,dir,explodePhase
Dim activeTanks as uByte
Dim tankFrame as uByte = TANK_SPEED
Dim tankProbability as uByte

Dim activeTowers as uByte 

Dim availableMines as uByte = 0
Dim availableBombs as uByte = 0
Dim availableBlockers as uByte = 0
Dim availableChargedShots as uByte = 0

Dim activeShips as uByte
Dim ships(MAX_SHIP_COUNT,4) as uByte 'x,y,dir,frame
Dim freezingShip as uByte = 1

Dim activeHangars as uByte
Dim hangars(MAX_HANGAR_COUNT,4) as uByte 'x,y,dir,frame
Dim freezingHangar as uByte = 1

init:

#include "ZoomAnims.zxbas"

InstallHandler()
InitFastRnd()

ink 7
paper 0
border 0
bright 1
cls

funcs:

sub GridObject(INDEX as ubyte, X as ubyte, Y as ubyte)
    activeMap(X,Y) = INDEX
    SetTileChecked(INDEX, tileColors(INDEX + 1), X + MAP_X_OFFSET, Y + MAP_Y_OFFSET)
end sub

sub ClearGridCell(X as ubyte, Y as ubyte)
    activeMap(X,Y) = 0
    SetTileChecked(GRID_INDEX + 4, $47, X + MAP_X_OFFSET, Y + MAP_Y_OFFSET)
end sub

function fastcall FindEmptyBit(InputByte as uByte) as uByte

    asm

        ld b, 0
febloop:
        inc b
        srl a
        jp c, febloop
        ld a, b

    end asm

end function

sub AddScore(ScoreToAdd as uinteger)
    score = score + ScoreToAdd
    updateScore = 1
end sub

sub ShowLifes()

    Dim buc as uByte

    for buc = 1 to 4
        if lifes >= buc then
            SetTile(LIFE_INDEX, tileColors(LIFE_INDEX + 1), 22 + (buc << 1), 22)
        else
            SetTile(EMPTY_TILE_INDEX, 0, 22 + (buc << 1), 22)
        end if
    next buc        

end sub

sub InitGraphics()

    InitGFXLib()
    SetTileset(@tileSet)

    /'
    Create1x1Sprite(PLAYER_ADDRESS)
    Create1x1Sprite(PLAYERRIGHT_ADDRESS)
    Create1x1Sprite(PLAYERLEFT_ADDRESS)
    Create1x1Sprite(VFIREA_ADDRESS)
    Create1x1Sprite(VFIREB_ADDRESS)
    Create1x1Sprite(HFIREA_ADDRESS)
    Create1x1Sprite(HFIREB_ADDRESS)
    Create1x1Sprite(EXPLODEA_ADDRESS)
    Create1x1Sprite(EXPLODEB_ADDRESS)
    Create1x1Sprite(EXPLODEC_ADDRESS)
    Create1x1Sprite(EXPLODED_ADDRESS)
    Create1x1Sprite(CURSOR_ADDRESS)
    Create1x1Sprite(ENEMA_ADDRESS)
    Create1x1Sprite(ENEMB_ADDRESS)
    Create1x1Sprite(TANKV_ADDRESS)
    Create1x1Sprite(TANKH_ADDRESS)
    '/

end sub

sub PrintPaddedNumber(x as ubyte, y as ubyte, value as uinteger, length as ubyte)

    Dim strnum as string = str$(value)

    Dim tmpVal as uByte = len(strnum)

    if tmpVal >= length then 
        strnum = strnum(1 TO length)
    else
         Dim lp as uByte

        for lp = 1 to (length - tmpVal)
            strnum = "0" + strnum
        next lp
    end if

    for tmpVal = 1 to length

        PRINT_DIGIT(x + tmpVal - 1, y, code(strnum(tmpVal to tmpVal)) - 48)

    next tmpVal

end sub

sub InitInterface()

    Dim tmpX as uByte
    Dim tmpY as uByte

    for tmpX = 0 to 31 step 4

        SetTile(LOGO_INDEX + tmpX, tileColors(LOGO_INDEX + tmpX + 1), (tmpX >> 2) + 23, 2)
        SetTile(LOGO_INDEX + tmpX + 1, tileColors(LOGO_INDEX + tmpX + 2), (tmpX >> 2) + 23, 3)
        SetTile(LOGO_INDEX + tmpX + 2, tileColors(LOGO_INDEX + tmpX + 3), (tmpX >> 2) + 23, 4)
        SetTile(LOGO_INDEX + tmpX + 3, tileColors(LOGO_INDEX + tmpX + 4), (tmpX >> 2) + 23, 5)

    next tmpX

    SetTile(BLOCKER_INDEX, tileColors(BLOCKER_INDEX + 1), 24, 18)
    SetTile(MINE_INDEX, tileColors(MINE_INDEX + 1), 28, 18)
    SetTile(BOMB_INDEX, tileColors(BOMB_INDEX + 1), 24, 20)
    SetTile(CHARGEDSHOT_INDEX, tileColors(CHARGEDSHOT_INDEX + 1), 28, 20)

    print at 18, 25;":"
    print at 18, 29;":"
    print at 20, 25;":"    
    print at 20, 29;":"

    print at 9, 24; "LEVEL:"
    print at 12, 24; "SCORE:"
    print at 15, 24; "SAT:"

end sub

sub DrawGrid()

    Dim tmpX as uByte
    Dim tmpY as uByte
    Dim tmpColor as uByte

    for tmpX = 1 to 20
        for tmpY = 1 to 20
            activeMap(tmpX, tmpY) = 0
        next tmpY
    next tmpX


    SetTile(GRID_INDEX, tileColors(GRID_INDEX + 1), 0, 1)
    SetTile(GRID_INDEX + 6, tileColors(GRID_INDEX + 7), 21, 1)
    SetTile(GRID_INDEX + 2, tileColors(GRID_INDEX + 3), 0, 22)
    SetTile(GRID_INDEX + 8, tileColors(GRID_INDEX + 9), 21, 22)

    for tmpX = 1 to 20

        SetTile(GRID_INDEX + 3, tileColors(GRID_INDEX + 4), tmpX, 1)
        SetTile(GRID_INDEX + 5, tileColors(GRID_INDEX + 6), tmpX, 22)

    next tmpX

    for tmpY = 2 to 21
    
        SetTile(GRID_INDEX + 1, tileColors(GRID_INDEX + 2), 0, tmpY)
        SetTile(GRID_INDEX + 7, tileColors(GRID_INDEX + 7), 21, tmpY)

        if tmpY = 21 then tmpColor = $46 else tmpColor = $47

        for tmpX = 1 to 20

            SetTile(GRID_INDEX + 4, tmpColor, tmpX, tmpY)

        next tmpX

    next tmpY

end sub

sub InitLevel()

    activeShots = 0
    activeAliens = 0
    activeTanks = 0
    activeShips = 0
    activeHangars = 0
    activeTowers = 0
    availableBlockers = 0
    availableBombs = 0
    availableMines = 0
    availableChargedShots = 0
    cursorTool = TOOL_NONE

    PRINT_DIGIT(26, 18, 0)
    PRINT_DIGIT(30, 18, 0)
    PRINT_DIGIT(26, 20, 0)
    PRINT_DIGIT(30, 20, 0)

    playerX = 20
    dead = 0
    tankProbability = 128

    
    PrintPaddedNumber(24, 10, 1, 2)
    updateScore = 0
    PrintPaddedNumber(24, 13, score, 6)

    
    print at 16, 24; "NONE   "

    shots(1,4) = -1
    shots(2,4) = -1
    shots(3,4) = -1

    
    Dim buc as Ubyte


    GridObject(SPLITTER_INDEX, 3, 3)
    GridObject(SPLITTER_INDEX, 17, 3)

    GridObject(DEFLECTORA_INDEX, 5, 10)
    GridObject(DEFLECTORB_INDEX, 5, 1)
    GridObject(DEFLECTORC_INDEX, 3, 1)
    GridObject(DEFLECTORD_INDEX, 15, 10)
    GridObject(DEFLECTORA_INDEX, 1, 3)
    GridObject(DEFLECTORB_INDEX, 1, 10)

    'for buc = 1 to 8
    '    GridObject(FORCEFIELD_INDEX, buc, 14)
    'next buc

    GridObject(HBARRIER_INDEX, 9, 11)
    GridObject(HBARRIER_INDEX + 1, 10, 11)
    GridObject(HBARRIER_INDEX + 1, 11, 11)
    GridObject(HBARRIER_INDEX + 2, 12, 11)

    GridObject(BIGBUILDING_INDEX, 9, 13)
    GridObject(BIGBUILDING_INDEX + 1, 10, 13)
    GridObject(BIGBUILDING_INDEX + 1, 11, 13)
    GridObject(BIGBUILDING_INDEX + 2, 12, 13)
    
    GridObject(BIGBUILDING_INDEX + 3, 9, 14)
    GridObject(BIGBUILDING_INDEX + 4, 10, 14)
    GridObject(BIGBUILDING_INDEX + 4, 11, 14)
    GridObject(BIGBUILDING_INDEX + 5, 12, 14)

    GridObject(BIGBUILDING_INDEX + 6, 9, 15)
    GridObject(BIGBUILDING_INDEX + 7, 10, 15)
    GridObject(BIGBUILDING_INDEX + 7, 11, 15)
    GridObject(BIGBUILDING_INDEX + 8, 12, 15)



    GridObject(MEDIUMBUILDING_INDEX, 1, 14)
    GridObject(MEDIUMBUILDING_INDEX + 1, 2, 14)
    GridObject(MEDIUMBUILDING_INDEX + 2, 3, 14)
    
    GridObject(MEDIUMBUILDING_INDEX + 3, 1, 15)
    GridObject(MEDIUMBUILDING_INDEX + 4, 2, 15)
    GridObject(MEDIUMBUILDING_INDEX + 5, 3, 15)

    GridObject(WIDEBUILDING_INDEX, 1, 17)
    GridObject(WIDEBUILDING_INDEX + 1, 2, 17)

    GridObject(TALLBUILDING_INDEX, 6, 17)
    GridObject(TALLBUILDING_INDEX + 1, 6, 18)


    for buc = 13 to 19
        GridObject(FORCEFIELD_INDEX, buc, 14)
    next buc

    GridObject(VBARRIER_INDEX, 20, 14)
    GridObject(VBARRIER_INDEX + 1, 20, 15)
    GridObject(VBARRIER_INDEX + 2, 20, 16)

    GridObject(MINE_INDEX, 10, 16)
    GridObject(MINE_INDEX, 11, 16)

    GridObject(BLOCKERCRATE_INDEX, 8, 17)
    GridObject(MINECRATE_INDEX, 10, 17)
    GridObject(BOMBCRATE_INDEX, 12, 17)
    GridObject(CHARGEDSHOTCRATE_INDEX, 14, 17)

    CREATE_TOWER(10, 10)

    GridObject(SMALLBUILDING_INDEX, 20, 7)
    GridObject(TOXICWASTE_INDEX, 13, 6)
    GridObject(TOXICWASTE_INDEX, 14, 6)
    GridObject(TOXICWASTE_INDEX, 15, 6)

    GridObject(EXPLOSIONCRATER_INDEX, 9, 5)
    GridObject(EXPLOSIONCRATER_INDEX + 1, 10, 5)
    GridObject(EXPLOSIONCRATER_INDEX + 2, 11, 5)
    
    GridObject(EXPLOSIONCRATER_INDEX + 3, 9, 6)
    GridObject(EXPLOSIONCRATER_INDEX + 4, 10, 6)
    GridObject(EXPLOSIONCRATER_INDEX + 5, 11, 6)

    GridObject(EXPLOSIONCRATER_INDEX + 6, 9, 7)
    GridObject(EXPLOSIONCRATER_INDEX + 7, 10, 7)
    GridObject(EXPLOSIONCRATER_INDEX + 8, 11, 7)

    CREATE_SHIP(7, 1, 3, buc)
    CREATE_SHIP(13, 1, 1, buc)

    CREATE_HANGAR(10, 1, 2, buc)

    ShowLifes()

end sub

sub UpdatePlayer()

    if dead <> 0 then return

    playerSprite = PLAYER_INDEX

    if MultiKeys(KEYZ) then
        dir = -1
        playerSprite = PLAYERLEFT_INDEX
        if playerX > 2 then playerX = playerX - 1
    else if MultiKeys(KEYC)
        dir = 1
        playerSprite = PLAYERRIGHT_INDEX
        if playerX < 40 then playerX = playerX + 1
    else
        if playerX & 1 = 0 then
            dir = 0
            playerSprite = PLAYER_INDEX
        else
            playerX = playerX + dir
        end if
    end if
end sub

sub CheckShotItems(shotIndex as uByte, cellX as uByte, cellY as uByte)

    Dim tmpX as uByte
    Dim tmpY as uByte
    Dim tmpDir as uByte

    if activeMap(cellX, cellY) then

        Dim tmpCell as uByte = activeMap(cellX, cellY)

        if shotIndex <> 255 then

            if tmpCell = SPLITTER_INDEX then

                if (activeShots bxor TABLE_LOOKUP(@maskTable, shotIndex)) = 0 then 'Only one active shot?

                    activeShots = 7 '111, all three shots
                    tmpX = shots(shotIndex, 1)
                    tmpY = shots(shotIndex, 2)
                    tmpDir = shots(shotIndex, 3) 

                    if tmpDir = 1 then
                        shots(1,3) = 1
                        shots(2,3) = 3
                        shots(3,3) = 4
                    else if tmpDir = 2 then
                        shots(1,3) = 2
                        shots(2,3) = 3
                        shots(3,3) = 4
                    else if tmpDir = 3 then
                        shots(1,3) = 1
                        shots(2,3) = 2
                        shots(3,3) = 3
                    else if tmpDir = 4 then
                        shots(1,3) = 1
                        shots(2,3) = 2
                        shots(3,3) = 4
                    end if

                    shots(1,1) = tmpX
                    shots(2,1) = tmpX
                    shots(3,1) = tmpX

                    shots(1,2) = tmpY
                    shots(2,2) = tmpY
                    shots(3,2) = tmpY

                else

                    EXPLODE_SHOT(shotIndex)

                end if

                return

            end if
        
            if tmpCell >= DEFLECTORA_INDEX then
            if tmpCell <= DEFLECTORD_INDEX then

            
                if tmpCell = DEFLECTORA_INDEX then
                    shots(shotIndex, 3) = TABLE_LOOKUP(@reflectDirsA, shots(shotIndex, 3))
                else if tmpCell = DEFLECTORB_INDEX then
                    shots(shotIndex, 3) = TABLE_LOOKUP(@reflectDirsB, shots(shotIndex, 3))
                else if tmpCell = DEFLECTORC_INDEX then
                    shots(shotIndex, 3) = TABLE_LOOKUP(@reflectDirsC, shots(shotIndex, 3))
                else
                    shots(shotIndex, 3) = TABLE_LOOKUP(@reflectDirsD, shots(shotIndex, 3))
                end if 

                'shots(shotIndex, 3) = reflectDirs(tmpCell - DEFLECTORA_INDEX + 1, shots(shotIndex, 3))
                return

            end if
            end if

            if tmpCell >= HBARRIER_INDEX then 
            if tmpCell <= EXPLOSIONCRATER_INDEX + 8 then

                EXPLODE_SHOT(shotIndex)

                return

            end if
            end if 
            
            if tmpCell >= FORCEFIELD_INDEX then
            if tmpCell <= TREES_INDEX then

                EXPLODE_SHOT(shotIndex)
                return
            
            end if
            end if

            if tmpCell >= MINECRATE_INDEX then
            if tmpCell <= CHARGEDSHOTCRATE_INDEX then

                ClearGridCell(cellX, cellY)
                CrateDestroyedEffect()
                EXPLODE_SHOT(shotIndex)
                AddScore(5)

                if tmpCell = MINECRATE_INDEX then

                    INCREASE_MINES()

                else if tmpCell = BOMBCRATE_INDEX then

                    INCREASE_BOMBS()

                else if tmpCell = BLOCKERCRATE_INDEX then

                    INCREASE_BLOCKERS()

                else if tmpCell = CHARGEDSHOTCRATE_INDEX then

                    INCREASE_SHOTS()

                end if

                return

            end if
            end if

        end if

        if tmpCell = RADIOTOWER_INDEX then
 
            ClearGridCell(cellX, cellY)

            if shotIndex <> 255 then 
                BuildTankDestroyedEffect()            
                EXPLODE_SHOT(shotIndex) 
            end if
            
            activeTowers = activeTowers - 1

            if activeTowers = 0 then 

                for tmpX = 1 to 20
                    for tmpY = 1 to 20
                        if activeMap(tmpX, tmpY) = FORCEFIELD_INDEX then 
                            ClearGridCell(tmpX, tmpY)
                        end if
                    next tmpY
                next tmpX

            end if

            return

        end if

        if tmpCell = MOTHERSHIP_INDEX then

            if shotIndex <> 255 then
                ClearGridCell(cellX, cellY)
                EXPLODE_SHOT(shotIndex)
                BuildTankDestroyedEffect()
            end if

            for tmpCell = 1 to MAX_SHIP_COUNT

                if ships(tmpCell, 1) = cellX then
                if ships(tmpCell, 2) = cellY then

                    activeShips = activeShips bxor TABLE_LOOKUP(@maskTable, tmpCell)
                    AddScore(50)
                    return

                end if
                end if

            next tmpCell
            
        end if

        if tmpCell = HANGAR_INDEX then

            ClearGridCell(cellX, cellY)

            if shotIndex <> 255 then
                EXPLODE_SHOT(shotIndex)
                BuildTankDestroyedEffect()
            end if

            for tmpCell = 1 to MAX_HANGAR_COUNT

                if hangars(tmpCell, 1) = cellX then
                if hangars(tmpCell, 2) = cellY then

                    activeHangars = activeHangars bxor TABLE_LOOKUP(@maskTable, tmpCell)
                    AddScore(75)
                    return

                end if
                end if

            next tmpCell
            
        end if

    end if

end sub

sub UpdateFire()

    if activeShots = 0 then

        if dir = 0 then
            if MultiKeys(KEYX)
                shots(1,1) = playerX
                shots(1,2) = 40
                shots(1,3) = 1
                activeShots = 1
            end if
        end if

    else

        Dim buc as byte
        Dim tmpX as byte
        Dim tmpY as byte
        Dim tmpDir as byte
        Dim tmpExplode as byte
        Dim tmpBounces as byte
        Dim tmpMask as uByte
        Dim tmpActive as uByte = activeShots

        for buc = 1 to 3

            tmpMask = TABLE_LOOKUP(@maskTable, buc)

            if tmpActive & tmpMask then  'x,y,dir,explodePhase
                
                tmpExplode = shots(buc, 4)

                if tmpExplode <> -1 then

                    tmpExplode = tmpExplode + 1

                    if tmpExplode = 4 then
                        tmpExplode = -1
                        activeShots = activeShots bxor tmpMask
                    end if

                    shots(buc, 4) = tmpExplode

                else

                    tmpX = shots(buc, 1)
                    tmpY = shots(buc, 2)
                    tmpDir = shots(buc, 3)

                    if tmpDir = 1 then                      'arriba
                        if tmpY <= 4 then 
                            shots(buc, 4) = 0
                            tmpY = 4
                        else
                            tmpY = tmpY - 1
                        end if
                    else if tmpDir = 2 then                 'abajo
                        if tmpY >= 42 then 
                            shots(buc, 4) = 0
                            tmpY = 42
                        else
                            tmpY = tmpY + 1
                        end if
                    else if tmpDir = 3 then                 'izquierda
                        if tmpX <= 2 then
                            shots(buc, 4) = 0
                            tmpX = 2
                        else
                            tmpX = tmpX - 1
                        end if
                    else if tmpDir = 4 then
                        if tmpX >= 40 then
                            shots(buc, 4) = 0
                            tmpX = 40
                        else
                            tmpX = tmpX + 1
                        end if
                    end if

                    shots(buc, 1) = tmpX
                    shots(buc, 2) = tmpY

                    if shots(buc, 4) = -1 then
                    if ((tmpX & 1) | (tmpY & 1)) = 0 then 
                        CheckShotItems(buc, ((tmpX >> 1) - MAP_X_OFFSET), ((tmpY >> 1) - MAP_Y_OFFSET))
                    end if
                    end if

                end if

            end if

        next buc

    end if

end sub

sub CheckShotUnits(DirectKill as ubyte, cellX as ubyte, cellY as ubyte, squareOffset as ubyte)

    Dim tmpMask as ubyte
    Dim buc as ubyte

    if not activeAliens and not activeTanks then return

    Dim xMin as ubyte = (((cellX - squareOffset) + MAP_X_OFFSET) << 1) - 1
    Dim xMax as ubyte = (((cellX + squareOffset) + MAP_X_OFFSET) << 1) + 1
    Dim yMin as ubyte = (((cellY - squareOffset) + MAP_Y_OFFSET) << 1) - 1
    Dim yMax as ubyte = (((cellY + squareOffset) + MAP_Y_OFFSET) << 1) + 1

    if activeAliens then

        for buc = 1 to MAX_ALIEN_COUNT

            tmpMask = TABLE_LOOKUP(@maskTable, buc)

            if activeAliens & tmpMask then

                if aliens(buc, 1) <= xMax then
                if aliens(buc, 1) >= xMin then
                if aliens(buc, 2) <= yMax then
                if aliens(buc, 2) >= yMin then

                    if DirectKill then
                        activeAliens = activeAliens bxor tmpMask
                        AddScore(10)
                    else
                        aliens(buc, 4) = 0
                    end if

                    continue for

                end if
                end if
                end if
                end if

            end if

        next buc

    end if                                

    if activeTanks then

        for buc = 1 to MAX_TANK_COUNT

            tmpMask = TABLE_LOOKUP(@maskTable, buc)

            if activeTanks & tmpMask then

                if tanks(buc, 1) <= xMax then
                if tanks(buc, 1) >= xMin then
                if tanks(buc, 2) <= yMax then
                if tanks(buc, 2) >= yMin then

                    if DirectKill then
                        activeTanks = activeTanks bxor tmpMask
                        AddScore(25)
                    else
                        tanks(buc, 4) = 0
                    end if

                    continue for

                end if
                end if
                end if
                end if
        
            end if

        next buc

    end if

end sub

sub CheckCursorAction(mapX as uByte, mapY as uByte)

    Dim tmpX as ubyte
    Dim tmpY as ubyte
    Dim buc as uByte
    Dim cnt as ubyte = 255
    Dim tmpMask as uByte

    if cursorTool = TOOL_NONE

        Dim tmpItem as uByte = activeMap(mapX, mapY)
        
        if not tmpItem then 
            return
        else if tmpItem = DEFLECTORA_INDEX then
            activeMap(mapX, mapY) = DEFLECTORB_INDEX
            tmpItem = 0
        else if tmpItem = DEFLECTORB_INDEX then
            activeMap(mapX, mapY) = DEFLECTORD_INDEX
            tmpItem = 0
        else if tmpItem = DEFLECTORD_INDEX then
            activeMap(mapX, mapY) = DEFLECTORC_INDEX
            tmpItem = 0
        else if tmpItem = DEFLECTORC_INDEX then
            activeMap(mapX, mapY) = DEFLECTORA_INDEX
            tmpItem = 0
        end if

        if tmpItem = 0 then
            SetTileChecked(activeMap(mapX, mapY), tileColors(DEFLECTORA_INDEX + 1), mapX + MAP_X_OFFSET, mapY + MAP_Y_OFFSET)
            DeflectorEffect()
        end if

    else if cursorTool = TOOL_MINE then

        if availableMines > 0 then

            if activeMap(mapX, mapY) = 0 then

                DECREASE_MINES()
                GridObject(MINE_INDEX, mapX, mapY)
                MinePlacedEffect()

            end if

        end if

    else if cursorTool = TOOL_BLOCKER then

        if availableBlockers > 0 then

            if activeMap(mapX, mapY) = 0 then

                DECREASE_BLOCKERS()
                GridObject(BLOCKER_INDEX, mapX, mapY)
                MinePlacedEffect()

            end if

        end if

    else if cursorTool = TOOL_BOMB then

        if availableBombs > 0 then

            DECREASE_BOMBS()
            BombEffect()

            CheckShotUnits(1, mapX, mapY, 1)

            for tmpY = mapY - 1 to mapY + 1
                for tmpX = mapX - 1 to mapX + 1
                    cnt = cnt + 1

                    if tmpX < 1  then continue for
                    if tmpY < 1  then continue for
                    if tmpX > 20  then continue for
                    if tmpX > 20  then continue for

                    CheckShotItems(255, tmpX, tmpY)
                    GridObject(EXPLOSIONCRATER_INDEX + cnt, tmpX, tmpY)
                next tmpX
            next tmpY

        end if

    else if cursorTool = TOOL_CHARGEDSHOT then

        if availableChargedShots > 0 then

            DECREASE_SHOTS()
            SATShotEffect()

            CheckShotItems(255, mapX, mapY)
            CheckShotUnits(0, mapX, mapY, 0)

        end if

    end if

end sub

sub UpdateCursor()

    if MultiKeys(KEY1) then

        cursorTool = TOOL_NONE
        print at 16, 24; "NONE   "
        
    else if MultiKeys(KEY2) then

        cursorTool = TOOL_BLOCKER
        print at 16, 24; "BARRIER"
        

    else if MultiKeys(KEY3) then

        cursorTool = TOOL_MINE
        print at 16, 24; "MINE   "

    else if MultiKeys(KEY4) then
        
        cursorTool = TOOL_BOMB
        print at 16, 24; "BOMB   "

    else if MultiKeys(KEY5) then

        cursorTool = TOOL_CHARGEDSHOT
        print at 16, 24; "LASER  "

    end if

    cursorFrames = cursorFrames - 1

    if cursorMoved = 0 or cursorFrames = 0 then

        if cursorX > 0 then
        if MultiKeys(KEYJ) then
            cursorX = cursorX - 1
            cursorMoved = 1
            cursorVisible = 1
            cursorFrames = CURSOR_SPEED
            goto CHECKCURSORFRAMES
        end if
        end if

        if cursorX < 19 then
        if MultiKeys(KEYL) then
            cursorX = cursorX + 1
            cursorMoved = 1
            cursorVisible = 1
            cursorFrames = CURSOR_SPEED
            goto CHECKCURSORFRAMES
        end if
        end if

        if cursorY > 0 then
        if MultiKeys(KEYI) then
            cursorY = cursorY - 1
            cursorMoved = 1
            cursorVisible = 1
            cursorFrames = CURSOR_SPEED
            goto CHECKCURSORFRAMES
        end if
        end if

        if cursorY < 19 then
        if MultiKeys(KEYK) then
            cursorY = cursorY + 1
            cursorMoved = 1
            cursorVisible = 1
            cursorFrames = CURSOR_SPEED
            goto CHECKCURSORFRAMES
        end if
        end if

        if MultiKeys(KEYSPACE) then
            CheckCursorAction(cursorX + 1, cursorY + 1)
            cursorMoved = 1
            cursorVisible = 1
            cursorFrames = CURSOR_SPEED
        else
            cursorMoved = 0
        end if

    else if AnyKey() <> 0 then
        cursorMoved = 0
    end if

CHECKCURSORFRAMES:

    if cursorFrames = 0 then
        cursorVisible = not cursorVisible
        cursorFrames = CURSOR_FRAMES
    end if

end sub

function LineOfSight(x as uinteger, y as uinteger) as ubyte

    Dim address as uinteger = @activeMap + ((x - 1) * 20) + y
    DIm buc as ubyte

    for buc = 1 to 19 - y
        if peek(address) = 0 or (peek(address) >= TOXICWASTE_INDEX and peek(address) <= FORCEFIELD_INDEX) then
            address = address + 1
        else
            return 0
        end if
    next buc

    return 1

end function

sub UpdateAliens()

    if activeAliens = 0 then return

    alienFrame = alienFrame - 1

    if alienFrame <> 0 then return

    alienFrame = ALIEN_SPEED

    Dim buc as uByte
    Dim tmpExplode as byte
    Dim tmpX as byte
    Dim tmpY as byte
    Dim tmpDir as byte
    Dim tmpCellX as byte
    Dim tmpCellY as byte
    Dim tmpMask as uByte
    Dim tmpCellValue as uByte

    for buc = 1 to MAX_ALIEN_COUNT

        tmpMask = TABLE_LOOKUP(@maskTable, buc)

        if activeAliens band tmpMask then 

            'Movimiento del enemigo:
            /'

                El enemigo tiende a moverse hacia un lado, cuando encuentra un obstáculo intenta bajar.
                Una vez baja invierte la dirección
                Si no puede bajar invierte tb dirección
                Nunca mueve hacia arriba

                Si invierte dirección sin bajar y no puede mover explota (si encerramos al enemigo se muere)
                
                Dir: derecha = 1
                     Izquierda = -1
    
            '/

            tmpExplode = aliens(buc, 4)

            if tmpExplode <> -1 then

                tmpExplode = tmpExplode + 1

                if tmpExplode = 4 then
                    tmpExplode = -1
                    activeAliens = activeAliens bxor tmpMask
                    AddScore(10)
                end if

                aliens(buc, 4) = tmpExplode

            else

                tmpX = aliens(buc, 1)
                tmpY = aliens(buc, 2)
                tmpDir = aliens(buc, 3)


                if tmpX & 1 then

                    'Estamos en media celda, el movimiento continúa
                    tmpX = tmpX + tmpDir

                else

                    tmpCellX = (tmpX >> 1) - MAP_X_OFFSET
                    tmpCellY = (tmpY >> 1) - MAP_Y_OFFSET

                    'probamos a seguir OPTIMIZAR
TESTALIENCONTINUE:

                    if tmpCellX > 1 then
                    if tmpDir = -1 then
                        goto ALIENCONTINUE
                    end if
                    end if

                    if tmpCellX < 20 then
                    if tmpDir = 1 then
                        goto ALIENCONTINUE
                    end if
                    end if
TESTALIENDOWN:
                    if tmpCellY < 20 then

                        tmpCellValue = activeMap(tmpCellX, tmpCellY + 1)

                        if tmpCellValue = 0 or (tmpCellValue >= FORCEFIELD_INDEX and tmpCellValue <= TREES_INDEX) then 'bajar
                            tmpDir = -tmpDir
                            tmpY = tmpY + 2 'verticalmente se mueve en celdas
                            goto ENDTESTCELL
                        else if tmpCellValue = MINE_INDEX then
                            ClearGridCell(tmpCellX, tmpCellY + 1)
                            aliens(buc, 4) = 0
                            goto ENDTESTCELL
                        end if

                    end if

TESTALIENINVERT:

                    if tmpCellX > 1 then
                    if tmpDir = 1 then
                        goto ALIENINVERT
                    end if
                    end if

                    if tmpCellX < 20 then
                    if tmpDir = -1 then
                        goto ALIENINVERT
                    end if
                    end if

EXPLODEALIEN:
                    aliens(buc, 4) = 0
                    goto ENDTESTCELL

ALIENCONTINUE:

                    tmpCellValue = activeMap(tmpCellX + tmpDir, tmpCellY)

                    if tmpCellValue = 0 or (tmpCellValue >= FORCEFIELD_INDEX and tmpCellValue <= TREES_INDEX) then
                        tmpX = tmpX + tmpDir
                        goto ENDTESTCELL
                    else if tmpCellValue = MINE_INDEX then
                        ClearGridCell(tmpCellX + tmpDir, tmpCellY)
                        aliens(buc, 4) = 0
                        goto ENDTESTCELL
                    end if

                    goto TESTALIENDOWN

ALIENINVERT:

                    tmpCellValue = activeMap(tmpCellX - tmpDir, tmpCellY) 

                    if tmpCellValue = 0 or (tmpCellValue >= FORCEFIELD_INDEX and tmpCellValue <= TREES_INDEX)  then 'invertir (en la última fila explota)                       
                        tmpDir = -tmpDir
                        tmpX = tmpX + tmpDir
                        goto ENDTESTCELL
                    else if tmpCellValue = MINE_INDEX then
                        ClearGridCell(tmpCellX - tmpDir, tmpCellY)
                        aliens(buc, 4) = 0
                        goto ENDTESTCELL
                    end if
        
                    goto EXPLODEALIEN

ENDTESTCELL:

                end if

                aliens(buc, 1) = tmpX
                aliens(buc, 2) = tmpY
                aliens(buc, 3) = tmpDir

                if tmpY = 42 then 
                    dead = 1
                else if aliens(buc, 4) = 0 then
                    KilledAlienEffect()
                end if

            end if

        end if

    next buc

end sub

sub UpdateTanks()

    if activeTanks = 0 then return

    tankFrame = tankFrame - 1

    if tankFrame <> 0 then return

    tankFrame = TANK_SPEED

    Dim buc as uByte
    Dim tmpExplode as byte
    Dim tmpX as byte
    Dim tmpY as byte
    Dim tmpDir as byte
    Dim tmpCellX as byte
    Dim tmpCellY as byte
    Dim tmpMask as uByte

    Dim upCellValue as byte
    Dim downCellValue as byte
    Dim leftCellValue as byte
    Dim rightCellValue as byte

    for buc = 1 to MAX_TANK_COUNT

        tmpMask = TABLE_LOOKUP(@maskTable, buc)

        if activeTanks band tmpMask then 

            tmpExplode = tanks(buc, 4)

            if tmpExplode <> -1 then

                tmpExplode = tmpExplode + 1

                if tmpExplode = 4 then

                    tmpExplode = -1
                    activeTanks = activeTanks bxor tmpMask
                    AddScore(25)

                end if

                tanks(buc, 4) = tmpExplode

            else

                tmpX = tanks(buc, 1)
                tmpY = tanks(buc, 2)
                tmpDir = tanks(buc, 3)

                tmpCellX = (tmpX >> 1) - MAP_X_OFFSET
                tmpCellY = (tmpY >> 1) - MAP_Y_OFFSET

                if tmpX + 1 >= playerX and tmpX - 1 <= playerX  then
                if LineOfSight((playerX >> 1) - MAP_X_OFFSET, tmpCellY) then
                    if FastRnd() <= tankProbability then
                        dead = 2
                        return
                    end if
                end if
                end if

                'movimientos tanke: gira en sentido de las agujas del reloj
                'Si queda bloqueado explota

                if (tmpX & 1) or (tmpY & 1) then

                    'Estamos en media celda, el movimiento continúa
                    if tmpDir = 1 then
                        tmpX = tmpX + 1
                    else if tmpDir = 2 then
                        tmpY = tmpY + 1
                    else if tmpDir = 3 then
                        tmpX = tmpX - 1
                    else if tmpDir = 4 then
                        tmpY = tmpY - 1
                    end if 

                    'if FastRnd() = 1 then
                    '    tmpDir = (FastRnd() band 3) + 1
                    'end if

                else

                    if tmpCellX > 1 then leftCellValue = activeMap(tmpCellX - 1, tmpCellY) else leftCellValue = -1
                    if tmpCellX < 20 then rightCellValue = activeMap(tmpCellX + 1, tmpCellY) else rightCellValue = -1
                    if tmpCellY > 1 then upCellValue = activeMap(tmpCellX, tmpCellY - 1) else upCellValue = -1
                    if tmpCellY < 19 then downCellValue = activeMap(tmpCellX, tmpCellY + 1) else downCellValue = -1

                    if (leftCellValue >= TOXICWASTE_INDEX and leftCellValue <= FLOOD_INDEX) or leftCellValue = FORCEFIELD_INDEX then leftCellValue = 0
                    if (rightCellValue >= TOXICWASTE_INDEX and rightCellValue <= FLOOD_INDEX) or rightCellValue = FORCEFIELD_INDEX then rightCellValue = 0
                    if (upCellValue >= TOXICWASTE_INDEX and upCellValue <= FLOOD_INDEX) or upCellValue = FORCEFIELD_INDEX then upCellValue = 0
                    if (downCellValue >= TOXICWASTE_INDEX and downCellValue <= FLOOD_INDEX) or downCellValue = FORCEFIELD_INDEX then downCellValue = 0

TESTTANKLOOP:

                    if leftCellValue = -1 then
                    if rightCellValue = -1 then
                    if upCellValue = -1 then
                    if downCellValue = -1 then
                        tanks(buc, 4) = 0
                        goto ENDTESTCELLTANK
                    end if
                    end if
                    end if
                    end if


                    if tmpDir = 1 then

                        if rightCellValue = 0 then
                            tmpX = tmpX + 1
                            goto ENDTESTCELLTANK
                        else 
                            if rightCellValue = MINE_INDEX then
                                BuildTankDestroyedEffect()
                                ClearGridCell(tmpCellX + 1, tmpCellY)
                                tanks(buc, 4) = 0
                                goto ENDTESTCELLTANK
                            else
                                rightCellValue = -1
                                tmpDir = 2
                                goto TESTTANKLOOP
                            end if
                        end if
                    end if

                    if tmpDir = 2 then

                        if downCellValue = 0 then
                            tmpY = tmpY + 1
                            goto ENDTESTCELLTANK
                        else 
                            if downCellValue = MINE_INDEX then
                                BuildTankDestroyedEffect()
                                ClearGridCell(tmpCellX, tmpCellY + 1)
                                tanks(buc, 4) = 0
                                goto ENDTESTCELLTANK
                            else
                                downCellValue = -1
                                tmpDir = 3
                                goto TESTTANKLOOP
                            end if
                        end if
                    end if

                    if tmpDir = 3 then

                        if leftCellValue = 0 then
                            tmpX = tmpX - 1
                            goto ENDTESTCELLTANK
                        else 
                            if leftCellValue = MINE_INDEX then
                                BuildTankDestroyedEffect()
                                ClearGridCell(tmpCellX - 1, tmpCellY)
                                tanks(buc, 4) = 0
                                goto ENDTESTCELLTANK
                            else
                                leftCellValue = -1
                                tmpDir = 4
                                goto TESTTANKLOOP
                            end if
                        end if
                    end if

                    if tmpDir = 4 then

                        if upCellValue = 0 then
                            tmpY = tmpY - 1
                            goto ENDTESTCELLTANK
                        else 
                            if upCellValue = MINE_INDEX then
                                BuildTankDestroyedEffect()
                                ClearGridCell(tmpCellX, tmpCellY - 1)
                                tanks(buc, 4) = 0
                                goto ENDTESTCELLTANK
                            else
                                upCellValue = -1
                                tmpDir = 1
                                goto TESTTANKLOOP
                            end if
                        end if
                    end if

ENDTESTCELLTANK:

                end if

                tanks(buc, 1) = tmpX
                tanks(buc, 2) = tmpY
                tanks(buc, 3) = tmpDir

                if tmpX + 1 >= playerX and tmpX - 1 <= playerX  then

                    tmpCellX = (tmpX >> 1) - MAP_X_OFFSET
                    tmpCellY = (tmpY >> 1) - MAP_Y_OFFSET

                    if LineOfSight((playerX >> 1) - MAP_X_OFFSET, tmpCellY) then
                        if FastRnd() <= tankProbability then
                            dead = 2
                            return
                        end if
                    end if

                end if

            end if

        end if

    next buc

end sub

sub TestCollisions()

    Dim buc as ubyte

    Dim tmpShot1XMin as Byte
    Dim tmpShot1YMin as Byte
    
    Dim tmpShot1XMax as Byte
    Dim tmpShot1YMax as Byte

    Dim tmpShot2XMin as Byte
    Dim tmpShot2YMin as Byte
    
    Dim tmpShot2XMax as Byte
    Dim tmpShot2YMax as Byte

    Dim tmpShot3XMin as Byte
    Dim tmpShot3YMin as Byte

    Dim tmpShot3XMax as Byte
    Dim tmpShot3YMax as Byte

    Dim activeShot1 as Byte
    Dim activeShot2 as Byte
    Dim activeShot3 as Byte

    Dim tmpEnemX as Byte
    Dim tmpEnemY as Byte

    Dim tmpMask as ubyte

    if activeShots then

        tmpShot1XMin = shots(1,1) - 1
        tmpShot1YMin = shots(1,2) - 1
        tmpShot1XMax = tmpShot1XMin + 2
        tmpShot1YMax = tmpShot1YMin + 2

        activeShot1 = activeShots band 1

        tmpShot2XMin = shots(2,1) - 1
        tmpShot2YMin = shots(2,2) - 1
        tmpShot2XMax = tmpShot2XMin + 2
        tmpShot2YMax = tmpShot2YMin + 2

        activeShot2 = activeShots band 2

        tmpShot3XMin = shots(3,1)
        tmpShot3YMin = shots(3,2)
        tmpShot3XMax = tmpShot3XMin + 2
        tmpShot3YMax = tmpShot3YMin + 2

        activeShot3 = activeShots band 4

        if activeAliens then

            for buc = 1 to MAX_ALIEN_COUNT

                if activeAliens & TABLE_LOOKUP(@maskTable, buc) then

                    tmpEnemX = aliens(buc, 1)
                    tmpEnemY = aliens(buc, 2)

                    if activeShot1 then
                    if tmpEnemX >= tmpShot1XMin then
                    if tmpEnemX <= tmpShot1XMax then
                    if tmpEnemY >= tmpShot1YMin then
                    if tmpEnemY <= tmpShot1YMax then

                        aliens(buc, 4) = 0
                        activeShots = activeShots band 11111110b
                        KilledAlienEffect()
                        continue for

                    end if
                    end if
                    end if
                    end if
                    end if

                    if activeShot2 then
                    if tmpEnemX >= tmpShot2XMin then
                    if tmpEnemX <= tmpShot2XMax then
                    if tmpEnemY >= tmpShot2YMin then
                    if tmpEnemY <= tmpShot2YMax then

                        aliens(buc, 4) = 0
                        activeShots = activeShots band 11111101b
                        KilledAlienEffect()
                        continue for

                    end if
                    end if
                    end if
                    end if
                    end if

                    if activeShot3 then
                    if tmpEnemX >= tmpShot3XMin then
                    if tmpEnemX <= tmpShot3XMax then
                    if tmpEnemY >= tmpShot3YMin then
                    if tmpEnemY <= tmpShot3YMax then

                        aliens(buc, 4) = 0
                        activeShots = activeShots band 11111011b
                        KilledAlienEffect()
                        continue for

                    end if
                    end if
                    end if
                    end if
                    end if

                end if 

            next buc

        end if

        if activeTanks then

            for buc = 1 to MAX_TANK_COUNT

                if activeTanks & TABLE_LOOKUP(@maskTable, buc) then

                    tmpEnemX = tanks(buc, 1)
                    tmpEnemY = tanks(buc, 2)

                    if activeShot1 then
                    if tmpEnemX >= tmpShot1XMin then
                    if tmpEnemX <= tmpShot1XMax then
                    if tmpEnemY >= tmpShot1YMin then
                    if tmpEnemY <= tmpShot1YMax then

                        tanks(buc, 4) = 0
                        activeShots = activeShots band 11111110b
                        BuildTankDestroyedEffect()
                        continue for

                    end if
                    end if
                    end if
                    end if
                    end if

                    if activeShot2 then
                    if tmpEnemX >= tmpShot2XMin then
                    if tmpEnemX <= tmpShot2XMax then
                    if tmpEnemY >= tmpShot2YMin then
                    if tmpEnemY <= tmpShot2YMax then

                        tanks(buc, 4) = 0
                        activeShots = activeShots band 11111101b
                        BuildTankDestroyedEffect()
                        continue for

                    end if
                    end if
                    end if
                    end if
                    end if

                    if activeShot3 then
                    if tmpEnemX >= tmpShot3XMin then
                    if tmpEnemX <= tmpShot3XMax then
                    if tmpEnemY >= tmpShot3YMin then
                    if tmpEnemY <= tmpShot3YMax then

                        tanks(buc, 4) = 0
                        activeShots = activeShots band 11111011b
                        BuildTankDestroyedEffect()
                        continue for

                    end if
                    end if
                    end if
                    end if
                    end if

                end if 

            next buc

        end if

        if activeShot1 then
        if 41 = tmpShot1YMin then
        if playerX - 1 = tmpShot1XMin then

            dead = 3
            activeShots = activeShots band 11111110b
            return

        end if
        end if
        end if

        if activeShot2 then
        if 41 = tmpShot2YMin then
        if playerX - 1 = tmpShot2XMin then

            dead = 3
            activeShots = activeShots band 11111101b
            return

        end if
        end if
        end if

        if activeShot3 then
        if 41 = tmpShot3YMin then
        if playerX - 1 = tmpShot3XMin then

            dead = 3
            activeShots = activeShots band 11111011b
            return

        end if
        end if
        end if

    end if
    
end sub

sub UpdateShips()

    if activeShips = 0 then return

    Dim buc as uByte
    Dim tmpVal as uByte
    Dim tmpX as uByte
    Dim tmpY as uByte
    Dim tmpDir as uByte

    for buc = freezingShip to MAX_SHIP_COUNT
    
        if activeShips & TABLE_LOOKUP(@maskTable, buc) then

            if ships(buc, 4) > 0 then
                ships(buc, 4) = ships(buc, 4) - 1
            else
                if AVAILABLE_SLOT(activeAliens, MAX_ALIEN_COUNT) then
                    
                    ships(buc, 4) = SHIP_SPEED
                    tmpX = ships(buc, 1)
                    tmpY = ships(buc, 2)
                    tmpVal = ships(buc, 3)
                    
                    if tmpVal = 1 then  'spawn derecha
                        tmpX = tmpX + 1
                        tmpDir = 1      'alien mueve derecha
                    else if tmpVal = 2 then 'abajo
                        tmpY = tmpY + 1
                        tmpDir = 1
                    else if tmpVal = 3 then
                        tmpX = tmpX - 1
                        tmpDir = -1
                    else if tmpVal = 4 then
                        tmpY = tmpY - 1
                        tmpDir = -1
                    end if
                
                    CREATE_ALIEN(tmpX, tmpY, tmpDir, tmpVal)
                    freezingShip = 1
                else
                    freezingShip = buc
                    return                                      'If a ship is ready to generate aliens but no free spot is available we freeze the generation timers
            
                end if
            end if
        end if

    next buc

end sub

sub UpdateHangars()

    if activeHangars = 0 then return

    Dim buc as uByte
    Dim tmpX as uByte
    Dim tmpY as uByte
    Dim tmpDir as uByte
    Dim tmpVal as uByte

    for buc = freezingHangar to MAX_HANGAR_COUNT
    
        if activeHangars & TABLE_LOOKUP(@maskTable, buc) then

            if hangars(buc, 4) > 0 then
                hangars(buc, 4) = hangars(buc, 4) - 1
            else
                if AVAILABLE_SLOT(activeTanks, MAX_TANK_COUNT) then
                    hangars(buc, 4) = HANGAR_SPEED
                    tmpX = hangars(buc, 1)
                    tmpY = hangars(buc, 2)
                    tmpDir = hangars(buc, 3)
                    
                    if tmpDir = 1 then  'spawn derecha
                        tmpX = tmpX + 1
                    else if tmpDir = 2 then 'abajo
                        tmpY = tmpY + 1
                    else if tmpDir = 3 then
                        tmpX = tmpX - 1
                    else if tmpDir = 4 then
                        tmpY = tmpY - 1
                    end if
                
                    CREATE_TANK(tmpX, tmpY, tmpDir, tmpVal)
                    freezingHangar = 1
                else
                    freezingHangar = buc
                    return                                      'If a hangar is ready to generate tank but no free spot is available we freeze the generation timers
                end if
            end if
        end if

    next buc

end sub

main:

InitGraphics()
InitInterface()
DrawGrid()
InitLevel()

Dim buc as uByte
Dim tmpLoop as ubyte

while(1)

    frameCounter = 0

    UpdateCursor()
    UpdateAliens()
    UpdateTanks()
    UpdateShips()
    UpdateHangars()
    UpdatePlayer()
    UpdateFire()
    TestCollisions()

    Draw1x1Sprite(playerSprite, playerX, 42)

    if activeShots <> 0 then

        for buc = 1 to 3
            
            if activeShots & TABLE_LOOKUP(@maskTable, buc) then

                if shots(buc, 4) <> -1 then
                    Draw1x1Sprite(shots(buc, 4) + EXPLODEA_INDEX, shots(buc, 1), shots(buc, 2))
                else
                    if shots(buc, 3) < 3 then
                         if shots(buc, 2) & 1 then 
                            Draw1x1Sprite(VFIREA_INDEX, shots(buc, 1), shots(buc, 2))
                        else
                            Draw1x1Sprite(VFIREB_INDEX, shots(buc, 1), shots(buc, 2))
                        end if
                    else
                        if shots(buc, 1) & 1 then 
                            Draw1x1Sprite(HFIREA_INDEX, shots(buc, 1), shots(buc, 2))
                        else
                            Draw1x1Sprite(HFIREB_INDEX, shots(buc, 1), shots(buc, 2))
                        end if
                    end if
                end if

            end if

        next buc

    end if

    if cursorVisible then
        Draw1x1Sprite(CURSOR_INDEX, 2 + (cursorX << 1), 4 + (cursorY << 1))
    end if

    if activeAliens <> 0 then

        for buc = 1 to MAX_ALIEN_COUNT

            if activeAliens & TABLE_LOOKUP(@maskTable, buc) then

                if aliens(buc, 4) <> -1 then
                    Draw1x1Sprite(aliens(buc, 4) + EXPLODEA_INDEX, aliens(buc, 1), aliens(buc, 2))
                else

                    if aliens(buc, 1) & 1 then 
                        Draw1x1Sprite(ENEMB_INDEX, aliens(buc, 1), aliens(buc, 2))
                    else
                        Draw1x1Sprite(ENEMA_INDEX, aliens(buc, 1), aliens(buc, 2))
                    end if

                end if

            end if
    
        next buc

    end if

    if activeTanks <> 0 then

        for buc = 1 to MAX_TANK_COUNT

            if activeTanks & TABLE_LOOKUP(@maskTable, buc) then

                if tanks(buc, 4) <> -1 then
                    Draw1x1Sprite(tanks(buc, 4) + EXPLODEA_INDEX, tanks(buc, 1), tanks(buc, 2))
                else

                    if tanks(buc, 3) & 1 then 
                        Draw1x1Sprite(TANKH_INDEX, tanks(buc, 1), tanks(buc, 2))
                    else
                        Draw1x1Sprite(TANKV_INDEX, tanks(buc, 1), tanks(buc, 2))
                    end if

                end if

            end if
    
        next buc

    end if

    RenderFrame()

    if updateScore then
        updateScore = 0
        PrintPaddedNumber(24, 13, score, 6)
    end if

    if dead <> 0 then

        if dead = 1 then
            PlayDeathAlien()
        else if dead = 2 then
            PlayDeathTank()
        else
            PlayDeathShot()
        end if

        lifes = lifes - 1
        ShowLifes()

        DrawGrid()
        InitLevel()

    end if

    while frameCounter < 2
    end while
    

end while